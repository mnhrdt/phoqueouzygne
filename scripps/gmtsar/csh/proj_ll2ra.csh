#!/bin/csh -f 
#  D. Sandwell 1/12/07
#  M. Wei 1/31/08
#
alias rm 'rm -f'
unset noclobber
#
#
#  project a grd file from lon/lat coordinates into range/azimuth coordinates
#  this version only works with GMT V4.0 and higher
#
#  Input:
#  trans.dat    - file generated by llt_grid2rat  (r a topo lon lat)
#  phase_ll.grd - a GRD file of phase or anything in longitude/latitude coordinates 
#
#  Output:
#  phase_ra.grd - a GRD file of phase in radar coordinates
#
# check for number of arguments
#
 if ($#argv < 3) then
  echo " "
  echo "Usage: proj_ll2ra.csh trans.dat phase_ll.grd phase_ra.grd" 
  echo " "
  echo "        trans.dat    - file generated by llt_grid2rat  (r a topo lon lat)"
  echo "        phase_ll.grd - a GRD file of phase or anything in lon/lat-coordinates"
  echo "        phase_ra.grd - output a GRD file in radar coordinates" 
  echo " "
  exit 1
 endif 

#
#  extract the phase in the r a positions
#
grd2xyz $2 -S -bos3 > llp
#
#   make grids of longitude and latitude versus range and azimuth
#
gmtconvert $1 -F3,4,0 -bid5 -bos3 > llr
gmtconvert $1 -F3,4,1 -bid5 -bos3 > lla
#
surface llr `minmax llp -I0.08333333333 -bis3` -bis3 -I.00083333333333 -T.50 -Gllr.grd -V
surface lla `minmax llp -I0.08333333333 -bis3` -bis3 -I.00083333333333 -T.50 -Glla.grd -V
#
grdtrack llp -Q -Gllr.grd -bis3 -bos4 > llpr 
grdtrack llpr -Q -Glla.grd -bis4 -bos5 > llpra 
#
# get the range, azimuth, phase columns and grid
#
gmtconvert llpra -bis5 -bos3 -F3,4,2 > rap
#
#
xyz2grd rap `minmax rap -I32/64 -bis3` -I32/64 -F -G$3 -bis3
#
# clean
#
rm ll* rap

