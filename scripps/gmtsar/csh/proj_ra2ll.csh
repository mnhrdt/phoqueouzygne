#!/bin/csh -f 
#  D. Sandwell 1/12/07
#
alias rm 'rm -f'
unset noclobber
#
#
#  project a grd file from range/azimuth coordinates into lon/lat coordinates
#  this version only works with GMT V4.0 and higher
#
#  Input:
#  trans.dat    - file generated by llt_grid2rat  (r a topo lon lat)
#  phase_ra.grd - a GRD file of phase or anything
#
#  Output:
#  phase_ll.grd - a GRD file of phase in longitude/latitude coordinates
#
# check for number of arguments
#
 if ($#argv < 3) then
  echo " "
  echo "Usage: proj_ra2ll.csh trans.dat phase.grd phase_ll.grd" 
  echo "        trans.dat    - file generated by llt_grid2rat  (r a topo lon lat)"
  echo "        phase_ra.grd - a GRD file of phase or anything" 
  echo "        phase_ll.grd - output file in lon/lat-coordinates" 
  echo " "
  exit 1
 endif 
#
#  extract the phase in the r a positions
#
grd2xyz $2 -S -bos3 > rap
#
#   make grids of longitude and latitude versus range and azimuth unless they already exist
#
if (! -f raln.grd || ! -f ralt.grd ) then
  gmtconvert $1 -F0,1,3 -bid5 -bos3 > raln
  gmtconvert $1 -F0,1,4 -bid5 -bos3 > ralt
#
surface raln `minmax rap -I16/32 -bis3` -bis3 -I16/32 -T.50 -Graln.grd -V
surface ralt `minmax rap -I16/32 -bis3` -bis3 -I16/32 -T.50 -Gralt.grd -V
endif
#
grdtrack rap -Q -Graln.grd -bis3 -bos4 > rapln
grdtrack rapln -Q -Gralt.grd -bis4 -bos5 > raplnlt
#
# get the lon, lat, phase columns and grid
#
gmtconvert raplnlt -bis5 -bos3 -F3,4,2 > llp
#
# use higher resolution for data with higher range resolution
#
set rng_samp_rate = `grep rng_samp_rate *.PRM | awk 'NR == 1 {printf("%d", $3)}'`
echo "rng_samp_rate = " $rng_samp_rate 
set PRF = `grep PRF *.PRM | awk 'NR == 1 {printf("%d", $3)}'`
echo "PRF = " $PRF
if ($rng_samp_rate > 35000000 && $PRF > 1000 ) then
   blockmedian llp `minmax llp -I.0416666666667 -bis3 ` -bis -bos -I.000416666666667 -F -V > llpb
   xyz2grd llpb `minmax llpb -I.0416666666667 -bis3 ` -I.000416666666667 -F -G$3 -bis3
else 
   blockmedian llp `minmax llp -I.0833333333334 -bis3 ` -bis -bos -I.000833333333334 -F -V > llpb 
   xyz2grd llpb `minmax llpb -I.0833333333334 -bis3 ` -I.000833333333334 -F -G$3 -bis3
endif
#
# clean
#
rm rap* llp llpb raln ralt

